test_name        = ${ "'" ~ (!"'" ~ ANY)* ~ "'" }
ticks            = ${ ASCII_DIGIT* }
hex_digit        = ${ ^"0x" ~ ASCII_HEX_DIGIT{1,2} }
random           = ${ ^"RANDOM" }
input            = _{ hex_digit | random }
interrupt        = ${ ^"INTERRUPT" }
random_interrupt = ${ ^"RANDOM INTERRUPT" }
random_reset     = ${ ^"RANDOM RESET" }
random_input     = ${ ^"RANDOM INPUT" }
stop             = ${ ^"STOP" }
no_stop          = ${ ^"NO STOP" }
error_stop       = ${ ^"ERROR STOP" }
no_error_stop    = ${ ^"NO ERROR STOP" }

out_fe           =  { ^"FE" ~ "=" ~ hex_digit }
out_ff           =  { ^"FF" ~ "=" ~ hex_digit }

fc               =  { ^"FC" ~ "=" ~ input }
fd               =  { ^"FD" ~ "=" ~ input }
fe               =  { ^"FE" ~ "=" ~ input }
ff               =  { ^"FF" ~ "=" ~ input }

expectation  = _{ stop | no_stop | error_stop | no_error_stop
                | out_fe | out_ff }
setting      = _{ random_input | random_reset | random_interrupt | interrupt
                | fc | fd | fe | ff }

expect_block =  { ^"EXPECT" ~ (expectation ~ ",")* ~ expectation }
for_block    =  { ^"FOR" ~ ticks }
with_block   =  { ^"WITH" ~ (setting ~ ",")* ~ setting }

test         =  { ^"TEST " ~ test_name ~ with_block? ~ for_block ~ expect_block? ~ ^"END" }
file         = _{ SOI ~ test* ~ EOI }

WHITESPACE = _{ " " | "\t" | "\r\n" | "\r" | "\n" }
